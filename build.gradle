buildscript {
    ext {
        micronautDocsVersion = "1.0.22"
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-docs:$grailsVersion"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.2"
        classpath "de.marcphilipp.gradle:nexus-publish-plugin:0.4.0"
        classpath 'io.github.groovylang.groovydoc:groovydoc-gradle-plugin:1.0.1'
        classpath "io.micronaut.docs:micronaut-docs-asciidoc-extensions:$micronautDocsVersion"
        classpath "io.micronaut.docs:micronaut-docs-gradle-plugins:$micronautDocsVersion"
        classpath "com.adarshr:gradle-test-logger-plugin:2.0.0"
    }
}

plugins {
    id 'com.github.hierynomus.license' version '0.15.0' apply false
}

repositories {
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
}

version project.projectVersion


ext {
    distInstallDir = file("$buildDir/dist-tmp")
    homeBinDir = file("bin")
    homeLibDir = file("lib")
    homeSrcDir = file("src")
}

subprojects { Project subproject ->

    version project.projectVersion
	group "io.micronaut.aws"
    ext {
        isConfiguration = subproject.projectDir.parentFile.name == 'configurations'
        isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")
    }

    
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    
    apply plugin:"groovy"
    apply plugin:"java-library"
    apply plugin: "com.adarshr.test-logger"

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    apply from:"https://raw.githubusercontent.com/micronaut-projects/micronaut-build/v1.1.5/publishing.gradle"

    jar {
        manifest {
            attributes('Automatic-Module-Name': "${subproject.group}.${subproject.name}".replaceAll('[^\\w\\.\\$_]', "_"))
            attributes('Implementation-Version': projectVersion)
            attributes('Implementation-Title': title)
        }
    }

    bintray.publish = true

    tasks.withType(Test) {
        jvmArgs '-Duser.country=US'
        jvmArgs '-Duser.language=en'
        testLogging {
            exceptionFormat = 'full'
        }
        afterSuite {
            System.out.print(".")
            System.out.flush()
        }

        reports.html.enabled = !System.getenv("GITHUB_ACTIONS")
        reports.junitXml.enabled = !System.getenv("GITHUB_ACTIONS")
    }

    testlogger {
        theme 'standard'
        showFullStackTraces true
        showStandardStreams false
        showPassedStandardStreams false
        showSkippedStandardStreams false
        showFailedStandardStreams true
    }

    configurations {
        documentation
        all {
            exclude group: "commons-logging"
            exclude group: "org.apache.logging.log4j"
            resolutionStrategy.eachDependency { DependencyResolveDetails details ->
                String group = details.requested.group
                if(group == 'org.codehaus.groovy') {
                    details.useVersion(groovyVersion)
                }
            }
        }
    }
    dependencies {
        annotationProcessor platform("io.micronaut:micronaut-bom:$micronautVersion")
        annotationProcessor "io.micronaut:micronaut-inject-java"
        annotationProcessor "io.micronaut.docs:micronaut-docs-asciidoc-config-props:1.0.22"

        api platform("io.micronaut:micronaut-bom:$micronautVersion")
        api "io.micronaut:micronaut-inject"

        documentation "org.codehaus.groovy:groovy-templates:$groovyVersion"
        documentation "org.codehaus.groovy:groovy-dateutil:$groovyVersion"

        testAnnotationProcessor platform("io.micronaut:micronaut-bom:$micronautVersion")
        testAnnotationProcessor "io.micronaut:micronaut-inject-java"

        testImplementation platform("io.micronaut:micronaut-bom:$micronautVersion")
        testImplementation "org.codehaus.groovy:groovy-test:$groovyVersion"
        testImplementation "cglib:cglib-nodep:3.3.0"
        testImplementation "org.objenesis:objenesis:3.1"
        testImplementation("org.spockframework:spock-core:$spockVersion") {
            exclude module:'groovy-all'
        }
        testImplementation "io.micronaut:micronaut-inject-groovy"

        testRuntimeOnly "ch.qos.logback:logback-classic:1.2.3"
    }

    groovydoc {
        classpath += project.configurations.documentation
    }

    task allDeps(type: DependencyReportTask) {}

    apply plugin: 'checkstyle'
    
    checkstyle {
        toolVersion = 8.10
        configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")

        // Per submodule
        maxErrors = 1
        maxWarnings = 10

        showViolations = true
    }

    checkstyleTest.enabled = false
}

apply from:"https://raw.githubusercontent.com/micronaut-projects/micronaut-docs/v$micronautDocsVersion/gradle/docs.gradle"

allprojects {
    apply plugin: 'idea'

    idea {
        module {
            outputDir file('build/classes/java/main')
            testOutputDir file('build/classes/groovy/test')
        }
    }
    apply from: rootProject.file('gradle/license.gradle')
}