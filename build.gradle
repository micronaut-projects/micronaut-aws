buildscript {
    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/micronaut/core-releases-local" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "io.micronaut.build:micronaut-gradle-plugins:2.0.0.RC10"
    }
}

subprojects { Project subproject ->

    repositories {
        maven { url "https://oss.jfrog.org/oss-snapshot-local" }
    }

    group "io.micronaut.aws"

    apply plugin: "io.micronaut.build.common"

    micronautBuild {
        enforcedPlatform = true
    }

    if (subproject.parent.name == "examples" || subproject.name == "examples") {
        return
    }

    apply plugin: "io.micronaut.build.dependency-updates"
    if (subproject.name != 'test-suite') {
        apply plugin: "io.micronaut.build.publishing"
    }

    dependencies {
        testImplementation("org.spockframework:spock-core:${spockVersion}") {
            exclude module:'groovy-all'
        }
        testImplementation "io.micronaut.test:micronaut-test-spock:$micronautTestVersion"        
    }

    test {
        useJUnitPlatform()
    }
}

apply plugin: "io.micronaut.build.docs"
apply plugin: "io.micronaut.build.dependency-updates"

