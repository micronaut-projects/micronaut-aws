buildscript {
    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/micronaut/core-releases-local" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "io.micronaut.build:micronaut-gradle-plugins:2.0.5"
    }
}

subprojects { Project subproject ->

    group "io.micronaut.aws"

    apply plugin: "io.micronaut.build.common"

    micronautBuild {
        enforcedPlatform = true
    }

    if (subproject.parent.name == "examples" || subproject.name == "examples") {
        return
    }

    apply plugin: "io.micronaut.build.dependency-updates"
    if (subproject.name != 'test-suite') {
        apply plugin: "io.micronaut.build.publishing"
    }

    repositories {
        jcenter()
    }

    dependencies {
        annotationProcessor "io.micronaut:micronaut-inject-java"
        annotationProcessor "io.micronaut:micronaut-graal"
        annotationProcessor "io.micronaut.docs:micronaut-docs-asciidoc-config-props:1.0.23"
        testAnnotationProcessor "io.micronaut:micronaut-inject-java"
        testImplementation("org.spockframework:spock-core:${spockVersion}") {
            exclude module:'groovy-all'
        }
        testImplementation "io.micronaut.test:micronaut-test-spock:$micronautTestVersion"
        testImplementation 'org.junit.vintage:junit-vintage-engine'
    }

    test {
        useJUnitPlatform()
    }
}

apply plugin: "io.micronaut.build.docs"
apply plugin: "io.micronaut.build.dependency-updates"

