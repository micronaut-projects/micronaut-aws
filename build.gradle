buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "io.micronaut.build.internal:micronaut-gradle-plugins:3.0.3"
    }
}

subprojects { Project subproject ->

    group "io.micronaut.aws"

    boolean isExampleOrBenchmark = subproject.name.contains("example")
    ext {
        skipDocumentation = true
    }

    if (subproject.name != 'aws-bom' && !isExampleOrBenchmark) {

        apply plugin: "io.micronaut.build.internal.common"
        apply plugin: "io.micronaut.build.internal.dependency-updates"

        if (subproject.name != 'test-suite') {
            apply plugin: "io.micronaut.build.internal.publishing"
        }

        repositories {
            jcenter()
        }

        dependencies {
            annotationProcessor "io.micronaut:micronaut-inject-java"
            annotationProcessor "io.micronaut:micronaut-graal"
            annotationProcessor "io.micronaut.docs:micronaut-docs-asciidoc-config-props:${micronautDocsVersion}"

            testAnnotationProcessor "io.micronaut:micronaut-inject-java"
            testImplementation("org.spockframework:spock-core:${spockVersion}") {
                exclude module: 'groovy-all'
            }
            testImplementation "io.micronaut.test:micronaut-test-spock:$micronautTestVersion"
            testImplementation 'org.junit.vintage:junit-vintage-engine'

        }
    } else if (isExampleOrBenchmark) {
        repositories {
            jcenter()
            mavenCentral()
        }
        apply plugin: 'java'

    } else {
        apply plugin: "java-platform"
        version projectVersion
        apply plugin: "io.micronaut.build.internal.dependency-updates"
        apply plugin: "io.micronaut.build.internal.publishing"

    }
}

apply plugin: "io.micronaut.build.internal.docs"
apply plugin: "io.micronaut.build.internal.dependency-updates"

