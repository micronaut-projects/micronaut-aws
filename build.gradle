subprojects { Project subproject ->

    group "io.micronaut.aws"

    if (subproject.name != 'aws-bom') {

        apply plugin: "io.micronaut.build.internal.common"

        repositories {
            maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
        }

        dependencies {
            annotationProcessor "io.micronaut:micronaut-inject-java"
            annotationProcessor "io.micronaut:micronaut-graal"
            if (!subproject.name.startsWith("test-suite")) {
                annotationProcessor "io.micronaut.docs:micronaut-docs-asciidoc-config-props:${micronautDocsVersion}"
            }

            testAnnotationProcessor "io.micronaut:micronaut-inject-java"
            testImplementation("org.spockframework:spock-core:${spockVersion}") {
                exclude module: 'groovy-all'
            }
            testImplementation "io.micronaut.test:micronaut-test-spock:$micronautTestVersion"
        }
    } else {
        apply plugin: "java-platform"
        version projectVersion
    }
    apply plugin: "io.micronaut.build.internal.dependency-updates"
    if (!subproject.name.startsWith("test-suite")) {
        apply plugin: "io.micronaut.build.internal.publishing"
    }
}

apply plugin: "io.micronaut.build.internal.docs"
apply plugin: "io.micronaut.build.internal.dependency-updates"

